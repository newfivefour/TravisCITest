apply plugin: 'com.android.application'

repositories {
  jcenter()
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    signingConfigs {
      // A keystore for the CI environment. Because the debug.keystore will change on each new
      // version of creation of the CI environment, and that makes updates via continuous 
      // deployment impossible.
      ci {
        keyAlias "alias_name"
        keyPassword System.getenv("CI_KEYSTORE_PASSWORD")
        storeFile file("my-ci-key.keystore")
        storePassword System.getenv("CI_KEYSTORE_PASSWORD")
      }
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "e1e4c963ad144f23a8787ac79c3d1954"]
        applicationId "com.newfivefour.traviscitest"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 4
        versionName "1.0"
    }

    buildTypes {
        // A variant for the CI environment. Because the debug.keystore will change on each new
        // version of creation of the CI environment, and that makes updates via continuous 
        // deployment impossible.
        debug_with_ci_keystore.initWith(buildTypes.debug)
        debug_with_ci_keystore {
            if(System.getenv("CI_KEYSTORE_PASSWORD")) signingConfig signingConfigs.ci // Only if we have the keystore password
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'net.hockeyapp.android:HockeySDK:4.0.1'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.1.1'
}
